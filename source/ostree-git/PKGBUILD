pkgname='ostree-git'
pkgver=2015.4
pkgrel=1
url="http://git.gnome.org/browse/ostree/tree/README.md"
pkgdesc='OSTree is a tool for managing bootable, versioned filesystem trees.'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
provides=('ostree')
conflicts=("ostree")
makedepends=('pkg-config' 'gtk-doc' 'git')
depends=('libarchive'
         'linux-user-chroot'
         'libsoup'
         'gpgme'
         'systemd'
         'libgsystem'
         'gobject-introspection')

source=("${pkgname}::git+git://git.gnome.org/ostree#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
	./autogen.sh \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/ \
		--sysconfdir=/etc \
		--with-soup \
		--with-gpgme \
		--with-systemd \
		--with-libarchive \
		--with-mkinitcpio \
		--without-dracut \
		--without-selinux \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--enable-gtk-doc
	make
}

package() {
  cd "${srcdir}/${pkgname}"
  make install DESTDIR="$pkgdir"
}
